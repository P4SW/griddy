options {
    OUTPUT_DIRECTORY = "src/com/company";
    MULTI = true;
    NODE_PACKAGE = "com.company.parser";
    JJTREE_OUTPUT_DIRECTORY = "src/com/company/parser";
    VISITOR = true;
    LOOKAHEAD = 1;
    NODE_DEFAULT_VOID = true;
    JDK_VERSION = "17.0";
}

PARSER_BEGIN(Griddy)
package com.company;

import java.util.ArrayList;
import java.io.InputStream;import java.util.HashMap;
import com.company.target.*;
import com.company.parser.GriddyVisitor;

public class Griddy {
    Target target = Target.C;
    public static void main(Target target, boolean tree, InputStream input, StringBuilder output) {
        Griddy parser = new Griddy(input);

        try {
            ASTStart n = parser.Start();
            if (tree) n.dump("");
            else {
                GriddyVisitor v;
                switch(target){
                    case JS : v = new JSVisitor(); break;
                    default : v = new CVisitor(); break;
                }
                n.jjtAccept(v, output);
            }
        } catch (Exception e) {
            System.out.println("An error occurred.");
            System.out.println(e.getMessage());
            e.printStackTrace();
        }
    }
}

PARSER_END(Griddy)

/* Whitespace characters */
SKIP : {
        < WHITE_SPACE:  <TAB> | <NEWLINE> | <SP>    >
    |   < #SP:   " "     >
    |   < #TAB:  "\t"    >
}

/* Comments */
SPECIAL_TOKEN : {
        <SINGLE_LINE_COMMENT:   "#" (~["\n","\r"])* <NEWLINE>   >
    |   < NEWLINE:  "\n" | "\r" | "\r\n"    >   // line endings
}

/* Reserved keywords */
TOKEN : {
        < IF:   "if"    >   // if statement
    |   < ELSE: "else"  >   // else statement
    |   < FROM: "from"  >   // get value 'from' board, e.g. '(x,y) from my_board'
    |   < ON:   "on"    >   // value exist on board, e.g. 'game_piece on my_board'
    |   < IN:   "in"    >   // e.g. 'for x in iterable'
    |   < FOR:  "for"   >   // for loop begin
    |   < END:  "end"   >   // block end
    |   < OUTPUT: "output"  >   // prints the output
    |   < GAME_SETUP:   "SETUP" >   // setup phase begin
    |   < GAME_MAIN:    "GAME"  >   // game phase begin
    |   < FUNC_DECL:    "fun"   >   // function declaration
    |   < BOARD_DECL:   "board" >   // board declaration token, e.g. 'board (n,m)'
    |   < PIECE_DECL:   "piece" >   // game piece declaration token, e.g. 'piece { ...props }'
}

/* Punctuators */
<DEFAULT, IN_REGEX>
TOKEN : {
        < EQ:   "=="    > : DEFAULT // equal
    |   < NEQ:  "!="    > : DEFAULT // 'not equal'
    |   < GEQ:  ">="    > : DEFAULT // 'greater than or equal to'
    |   < LEQ:  "<="    > : DEFAULT // 'less than or equal to'
    |   < NOT:  "!"     > : DEFAULT // 'not' / 'negate'
    |   < LT:   "<"     > : DEFAULT // 'less than'
    |   < GT:   ">"     > : DEFAULT // 'greater than'
    |   < AND:  "&&"    > : DEFAULT // logical 'and'
    |   < OR:   "||"    > : DEFAULT // logical 'or'
    |   < PLUS: "+"     > : DEFAULT // addition
    |   < MINUS:"-"     > : DEFAULT // subtraction
    |   < MULT: "*"     > : DEFAULT // multiplication
    |   < DIV:  "/"     > : DEFAULT // division
    |   < MOD:  "%"     > : DEFAULT // modulus
    |   < INCR:  "++"   > : DEFAULT // increment
    |   < DECR:  "--"   > : DEFAULT // decrement
    |   < ASSIGN: "="   > : DEFAULT // variable assigment
    |   < PLUS_ASSIGN:  "+="    > : DEFAULT // add and assign
    |   < MINUS_ASSIGN: "-="    > : DEFAULT // subtract and assign
    |   < MULT_ASSIGN:  "*="    > : DEFAULT // multiply and assign
    |   < DIV_ASSIGN:   "/="    > : DEFAULT // divide and assign
    |   < DOT:      "." > : DEFAULT     // dot (struct property access maybe?)
    |   < COMMA:    "," > : DEFAULT     // seperator comma
    |   < SEMICOLON: ";"> : DEFAULT     // statement endings
    |   < COLON:    ":" > : DEFAULT     // property assignment
    |   < LPAREN:   "(" > : DEFAULT     // left parenthesis
    |   < RPAREN:   ")" > : IN_REGEX    // right parenthesis
    |   < LBRACE:   "{" > : DEFAULT     // left curly bracket
    |   < RBRACE:   "}" > : IN_REGEX    // right curly bracket
    |   < LBRACKET: "[" > : DEFAULT     // left square bracket
    |   < RBRACKET: "]" > : IN_REGEX    // right square bracket
}

/* Literals (terminals) */
<DEFAULT, IN_REGEX>
TOKEN : {
        < BOOL: "true" | "false"    > : IN_REGEX
    |   < NULL: "empty number" | "empty string" > : DEFAULT
    |   < NULL_NUM: "number" > : DEFAULT
    |   < NULL_STR: "string" > : DEFAULT
    |   < NUM:  ["0"-"9"] | ["1"-"9"] (["0"-"9"])+  > : DEFAULT
    |   < NEG_NUM:  "-" <NUM>   > : DEFAULT
    |   < STRING:  "\"" (~["\"","\r","\n"])* "\"" > : DEFAULT // string literal
}

/* Identifier (terminals) */
TOKEN : {
        < IDENT:        <IDENT_PART> (<IDENT_PART>)* >
    |   < #IDENT_PART:  (<LETTER> | "_")+ >
    |   < #LETTER:      ["a"-"z","A"-"Z"] >
}

ASTStart Start() #Start : {}
{
    setupPhase()
    gamePhase()
    <EOF>
    { return jjtThis; }
}

void setupPhase() #Setup : {}
{
    <GAME_SETUP>
    (setupStmt())*
}

void setupStmt() : {}
{
        assignment() <SEMICOLON>
 }

void gamePhase() #Game : {}
{
    <GAME_MAIN>
    (gameStmt())+
}

void gameStmt() : {}
{
        assignment() <SEMICOLON>
    |   outputStmt() <SEMICOLON>
}

void boardDeclaration() #Board :
{
    Token a, b;
    ArrayList<Integer> boardSize = new ArrayList<Integer>();
}
{
    <BOARD_DECL> <LPAREN> a=<NUM> <COMMA> b=<NUM> <RPAREN>
    {
        boardSize.add(Integer.parseInt(a.image));
        boardSize.add(Integer.parseInt(b.image));
        jjtThis.jjtSetValue(boardSize);
    }
}

void pieceDeclaration() #Piece :
{
    HashMap<Node, Node> props = new HashMap<Node, Node>();
}
{
    <PIECE_DECL> <LBRACE>
    (
        { Node k, v; }
        k=identifier() <COLON> (v=expression() | v=stringLiteral())
        {
            props.put(k, v);
            jjtThis.jjtSetValue(props);
        }
    )*
    <RBRACE>
}

void assignment() #Assign : {}
{
    identifier() <ASSIGN>
    (   boardDeclaration()
    |   pieceDeclaration()
    |   expression()
    |   stringLiteral()
    |   empty()
    )
}

ASTEmpty empty() #Empty :
{
    Token t;
}
{
    t=<NULL>
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

ASTExpr expression() #Expr(>1) : {}
{
    multiplicativeExpr() ( ( additiveOperator() | booleanOperator() ) multiplicativeExpr() )*
    { return jjtThis; }
}

ASTBoolExpr boolExpression() #BoolExpr(>1) :
{
    Token t;
}
{
    multiplicativeExpr() (booleanOperator() multiplicativeExpr())*
    {return jjtThis;}
}

ASTOperator additiveOperator() #Operator :
{
    Token t;
}
{
    (t=<PLUS> | t=<MINUS>)
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

void multiplicativeExpr() #Expr(>1) : {}
{
    unary() ( multiplicativeOperator() unary() )*
}

ASTOperator multiplicativeOperator() #Operator :
{
    Token t;
}
{
    (t=<MULT> | t=<DIV> | t=<MOD>)
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

void unary() : {}
{
        <LPAREN> expression() <RPAREN>
    |   number()
    |   bool()
    |   identifier()
}

ASTOperator booleanOperator() #Operator :
{
    Token t;
}
{
    (  t=<EQ>
    |  t=<NEQ>
    |  t=<LEQ>
    |  t=<GEQ>
    |  t=<LT>
    |  t=<GT>
    |  t=<OR>
    )
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

ASTIdent identifier() #Ident :
{
    Token t;
}
{
    t=<IDENT>
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

ASTInteger number() #Integer :
{
    Token t;
}
{
    t=<NUM>
    {
        jjtThis.jjtSetValue(Integer.parseInt(t.image));
        return jjtThis;
    }
}

ASTBool bool() #Bool :
{
    Token t;
}
{
    t=<BOOL>
    {
        jjtThis.jjtSetValue(t.image);
        return jjtThis;
    }
}

ASTString stringLiteral() #String :
{
    Token t;
}
{
    t=<STRING>
    {
        jjtThis.jjtSetValue(t.image.substring(1, (t.image.length() - 1)));
        return jjtThis;
    }
}

ASTOutput outputStmt() #Output : {}
{
    <OUTPUT> ( expression() | stringLiteral() )
    {
        return jjtThis;
    }
}